# Maintainer: sheepla <hk7g9x43 at anonaddy dot me>

pkgname=ttf-plemol-jp
pkgver=1.4.0
pkgrel=3
pkgdesc="A Japanese programming font based on IBM Plex Sans and IBM Plex Mono"
url="https://github.com/yuru7/PlemolJP"
license=("custom:OFL" "MIT")
arch=("any")
source=(
  "${pkgname}-${pkgver}.zip::${url}/releases/download/v${pkgver}/PlemolJP_v${pkgver}.zip"
  "${pkgname}-${pkgver}-hs.zip::${url}/releases/download/v${pkgver}/PlemolJP_HS_v${pkgver}.zip"
  "${pkgname}-${pkgver}-nf.zip::${url}/releases/download/v${pkgver}/PlemolJP_NF_v${pkgver}.zip"
  "LICENSE::https://raw.githubusercontent.com/yuru7/PlemolJP/v${pkgver}/LICENSE"
  "README.md::https://raw.githubusercontent.com/yuru7/PlemolJP/v${pkgver}/README.md"
)

sha256sums=(
    'a416a6da71630edfab4fa9dd473920eba8a382e30f81f43f4ea3429217a1a989'
    '980be30e550e466503a3552540e565702344e47515cd504b76e67a16a5980d9d'
    '59f23d3795dae9dc62e3e356829cd496e0bdea8efefe8d7953a98ec5e85c6580'
    'SKIP'
    'SKIP'
)

package() {
  cd "${srcdir}"

  _ttf_dir="${pkgdir}/usr/share/fonts/TTF"
  install -d "${_ttf_dir}"
  for _font in $(find "${srcdir}" -name '*.ttf' -type f); do
    install -m644 "${_font}" "${_ttf_dir}"
  done

  _licenses_dir="${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${_licenses_dir}"
  install -m644 LICENSE "${_licenses_dir}/LICENSE"

  _doc_dir="${pkgdir}/usr/share/doc/${pkgname}"
  install -d "${_doc_dir}"
  install -m644 README.md "${_doc_dir}/README.md"
}
